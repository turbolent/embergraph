# BSBM BI Q6. Very fast.
#
# Query 5: Show the most popular products of a specific product type for each country - by review count
#
# Use Case Motivation: For advertisement reasons the owners of the e-commerce platform want to generate profiles for the two dimensions product type and the country of a customer.
#
# This is very fast (127ms) as long as the sub-select is extracted and run as 
# a named subquery.

prefix bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>
prefix bsbm-inst: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/>
prefix rev: <http://purl.org/stuff/rev#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>

Select ?reviewer (avg(xsd:float(?score)) As ?reviewerAvgScore)
{
  { Select (avg(xsd:float(?score)) As ?avgScore)
    {
      ?product bsbm:producer <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/dataFromProducer1/Producer1> .
      ?review bsbm:reviewFor ?product .
      { ?review bsbm:rating1 ?score . } UNION
      { ?review bsbm:rating2 ?score . } UNION
      { ?review bsbm:rating3 ?score . } UNION
      { ?review bsbm:rating4 ?score . }
    }
  }
  ?product bsbm:producer <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/dataFromProducer1/Producer1> .
  ?review bsbm:reviewFor ?product .
  ?review rev:reviewer ?reviewer .
  { ?review bsbm:rating1 ?score . } UNION
  { ?review bsbm:rating2 ?score . } UNION
  { ?review bsbm:rating3 ?score . } UNION
  { ?review bsbm:rating4 ?score . }
}
Group By ?reviewer
Having (avg(xsd:float(?score)) > min(?avgScore) * 1.5)
