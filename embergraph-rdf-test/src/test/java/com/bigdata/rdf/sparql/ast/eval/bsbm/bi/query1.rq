# BSBM BI Q1.  This query is "fixed" by optimistic=.67.
#
# Query 1: The top 10 most discussed product categories of products from a specific country based on number of reviews by reviewers from a certain country
#
# Use Case Motivation: A vendor wants to find out which product categories get the most attention by people from a certain country
#
# Last Changed Rev: 5682
# Last Changed Date: 2011-11-17 20:09:12 -0500 (Thu, 17 Nov 2011)
#
# w/o optimistic join order hint.
# 1. solutions=10, chunks=1, elapsed=34158ms.
# 2. solutions=10, chunks=1, elapsed=33888ms.
# 3. solutions=10, chunks=1, elapsed=33827ms.
#
# w/ optimistic=.67 join order hint
# solutions=10, chunks=1, elapsed=4135ms.
# solutions=10, chunks=1, elapsed=4202ms.
# solutions=10, chunks=1, elapsed=4143ms.

prefix bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>
prefix rev: <http://purl.org/stuff/rev#>

Select ?productType ?reviewCount
{
hint:Query hint:optimistic ".67" .
 { Select ?productType (count(?review) As ?reviewCount)
  {
   ?productType a bsbm:ProductType .
   ?product a ?productType .
   ?product bsbm:producer ?producer .
   ?producer bsbm:country <http://downlode.org/rdf/iso-3166/countries#RU> .
   ?review bsbm:reviewFor ?product .
   ?review rev:reviewer ?reviewer .
   ?reviewer bsbm:country <http://downlode.org/rdf/iso-3166/countries#US> .
  }
  Group By ?productType
 }
}
Order By desc(?reviewCount) ?productType
Limit 10
