# BSBM BI Q3
#
# Query 3: Products with largest increase of interest (ratio of review counts) from one month to the next.
#
# Use Case Motivation: Some stakeholder wants to get a list of products with the highest increase in popularity in a certain period for further investigation.
#
# Last Changed Rev: 5682
# Last Changed Date: 2011-11-17 20:09:12 -0500 (Thu, 17 Nov 2011)
# solutions=10, chunks=1, elapsed=87239ms.
# solutions=10, chunks=1, elapsed=84904ms.
#
# This query begs for a RangeBOp to handle the constraint on the date range.
# Right now it winds up running these predicates once in each subquery and
# then filtering.  If we were going to run it that way we would be better 
# off extracting the common joins into a named subquery.
# 
#       ?review bsbm:reviewFor ?product .
#       ?review dc:date ?date .
	
  prefix bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>
  prefix bsbm-inst: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/>
  prefix rev: <http://purl.org/stuff/rev#>
  prefix dc: <http://purl.org/dc/elements/1.1/>
  prefix xsd: <http://www.w3.org/2001/XMLSchema#>
  Select ?product (xsd:float(?monthCount)/?monthBeforeCount As ?ratio)
  {
    { Select ?product (count(?review) As ?monthCount)
      {
        ?review bsbm:reviewFor ?product .
        ?review dc:date ?date .
        Filter(?date >= "2008-02-02"^^<http://www.w3.org/2001/XMLSchema#date> && ?date < "2008-03-01"^^<http://www.w3.org/2001/XMLSchema#date>)
      }
      Group By ?product
    }  {
      Select ?product (count(?review) As ?monthBeforeCount)
      {
        ?review bsbm:reviewFor ?product .
        ?review dc:date ?date .
        Filter(?date >= "2008-01-05"^^<http://www.w3.org/2001/XMLSchema#date> && ?date < "2008-02-02"^^<http://www.w3.org/2001/XMLSchema#date>) #
      }
      Group By ?product
      Having (count(?review)>0)
    }
  }
  Order By desc(xsd:float(?monthCount) / ?monthBeforeCount) ?product
  Limit 10
