# BSBM BI Q2.  Q2 needs further exploration. See my notes.
#
# Query 2: The top 10 products most similar to a specific product, rated by the count of features they have in common.
#
# Use Case Motivation:  A consumer wants to list similar products to the product they are viewing right now.
#
# Last Changed Rev: 5682
# Last Changed Date: 2011-11-17 20:09:12 -0500 (Thu, 17 Nov 2011)
#
# 1. solutions=10, chunks=1, elapsed=102699ms.
# 2. solutions=10, chunks=1, elapsed=89905ms.
# 3. solutions=10, chunks=1, elapsed=86678ms.
#
prefix bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>

SELECT ?otherProduct ?sameFeatures
{
  ?otherProduct a bsbm:Product .
  FILTER(?otherProduct != <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/dataFromProducer1/Product6>)
  {
    SELECT ?otherProduct (count(?otherFeature) As ?sameFeatures)
    {
      <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/dataFromProducer1/Product6> bsbm:productFeature ?feature .
      ?otherProduct bsbm:productFeature ?otherFeature .
      FILTER(?feature=?otherFeature)
    }
    Group By ?otherProduct
  }
}
Order By desc(?sameFeatures) ?otherProduct
Limit 10
